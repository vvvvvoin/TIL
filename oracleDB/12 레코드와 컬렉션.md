# 레코드와 컬렉션

- 오라클 DB에서는 한 번에 여러 데이터를 관리하거나 저장하기 위해 레코드, 컬렉션 자료형을 제공한다.

## 자료형이 다른 여러 데이터를 저장하는 레코드

### 레코드란?

- 레코드는 자료형이 각기 다른 데이터를 하나의 변수에 저장하는 데 사용한다.
- 기본형식은 다음과 같다.

```SQL
TYPE 레코드이름 IS RECORD(
	변수이름 자료형 NOT NULL :=orDEFAULT 값 또는 값이 도출되는 여러 표현식
)
```

- 정의한 레코드는 다른 변수와 마찬가지로 기존 자료형 처럼 사용할 수 있다.
- 레코드에 포함된 변수는 레코드 이름과 마침표로 사용할 수 있다.

```SQL
DECLARE
	TYPE REC_DEPT IS RECORD(
    	deptno NUMBER(2) NOT NULL := 99,
        dname DEPT.DNAME%TYPE,
        loc DEPT.LOC%TYPE
    );
    dept_rec REC_DEPT;
BEGIN
	dept_rec.deptno := 99;
	dept_rec.dname := 'DATABASE';
	dept_rec.loc := 'SEOUL';
	DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || dept_rec.deptno);
	DBMS_OUTPUT.PUT_LINE('DNAME : ' || dept_rec.dname);
	DBMS_OUTPUT.PUT_LINE('LOC : ' || dept_rec.loc);
END;
/
```

### 레코드를 사용한 INSERT

- PL/SQL 문에서는 테이블에 데이터를 삽입하거나 수정하는 INSERT, UPDATE문에도 레코드를 사용할 수 있다.
- 레코드를 사용할 테이블을 먼저 만든다.

```SQL
CREATE TABLE DEPT_RECORD
AS SELECT * FROM DEPT;

SELECT * FROM DEPT_RECORD;
```

- 기존 INSERT문에서는 삽입할 데이터를 VALUES절에 하나씩 명시하였다.
- 하지만 INSERT문에 레코드를 사용하면 VALUES절에 레코드 이르만 명시하면 된다.
- 선언한 레코드와 INSERT 대상이 되는 테이블의 데이터 개수, 자료형, 순서를 맞추어야 한다.

```SQL
DECLARE
	TYPE REC_DEPT IS RECORD(
    	deptno NUMBER(2) NOT NULL := 99,
        dname DEPT.DNAME%TYPE,
        loc DEPT.LOC%TYPE
    );
    dept_rec REC_DEPT;
BEGIN
	dept_rec.deptno := 99;
	dept_rec.dname := 'DATABASE';
	dept_rec.loc := 'SEOUL';
	
INSERT INTO DEPT_RECORD VALUES dept_rec;
END;
/

SELECT * FROM DEPT_RECORD;
```

### 레코드를 사용한 UPDATE

- 레코드는 UPDATE문에서도 사용할 수 있다.
- 이 경우에 SET절은 ROW 키워드와 함께 레코드 이름을 명시한다.
- 기존 UPDATE문에서는 SET절을 통해 변경할 열을 하나하나 지정한 것과 달리 레코드에 저장된 데이터를 사용하여 행 전체의 데이터를 바꿔 준다.

```SQL
DECLARE
	TYPE REC_DEPT IS RECORD(
    	deptno NUMBER(2) NOT NULL := 99,
        dname DEPT.DNAME%TYPE,
        loc DEPT.LOC%TYPE
    );
    dept_rec REC_DEPT;
BEGIN
	dept_rec.deptno := 50;
	dept_rec.dname := 'DB';
	dept_rec.loc := 'SEOUL';
	
	UPDATE DEPT_RECORD
	SET ROW = dept_rec
	WHERE DEPTNO = 99;
END;
/

SELECT * FROM DEPT_RECORD;
```

### 레코드를 포함하는 레코드

- 레코드에 포함된 변수의 자료형을 지정할 때 다른 레코드를 지정할 수도 있다.
- 두개의 레코드에 한 레코드에  변수를 만들고 자료형을 다른 레코드의 자료형을 지정한다.
- 레코드 역시 자료형이기 때문에 가능하다.
- 변수에 레코드형을 적용했으므로 두 개의 마침표로 값을 사용한다.

```sql
DECLARE
	TYPE REC_DEPT IS RECORD(
    	deptno NUMBER(2) NOT NULL := 99,
        dname DEPT.DNAME%TYPE,
        loc DEPT.LOC%TYPE
    );
    TYPE REC_EMP IS RECORD(
    	empno EMP.EMPNO%TYPE,
        ename EMP.ENAME%TYPE,
        dinfo REC_DEPT
    );
    emp_rec REC_EMP;
BEGIN
	SELECT E.EMPNO, E.ENAME, D.DEPTNO, D.DNAME, D.LOC
	INTO emp_rec.empno, emp_rec.ename, emp_rec.dinfo.deptno, emp_rec.dinfo.dname, emp_rec.dinfo.loc
	FROM EMP E, DEPT D
	WHERE E.DEPTNO = D.DEPTNO
	AND E.EMPNO = 7788;
	
	DBMS_OUTPUT.PUT_LINE('EMPNO : ' || emp_rec.empno);
	DBMS_OUTPUT.PUT_LINE('ENAME : ' || emp_rec.ename);
	DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || emp_rec.dinfo.deptno);
	DBMS_OUTPUT.PUT_LINE('DANAME : ' || emp_rec.dinfo.dname);
	DBMS_OUTPUT.PUT_LINE('LOC : ' || emp_rec.dinfo.loc);
END;
/
```

## 자료형이 같은 여러 데이터를 저장하는 컬렉션

- 컬렉션은 특정 자료형의 데이터를 여러 개 저장하는 복합 자료형이다.
- 여러 종류의 데이터를 하나로 묶어 사용하는 레코드를 테이블의 한 행 처럼 사용한다면, 컬렉션은 열 또는 테이블과 같은 형태로 사용할 수 있다.
- PL/SQL 에서 사용할 수 있는 컬렉션은 세가지 종류가 있다
  - 연관배열
  - 중첩 테이블
  - VARRAY

### 연관배열

- 연관배열은 인덱스라고도 불리는 키, 값으로 구성되는 컬렉션이다.
- 중복되지 않은 유일한 키를 통해 값을 저장하고 불러오는 방식이다.
- 연관배열을 정의할 때 자료형이 TABLE인 변수를 다음과 같이 작성한다.

```SQL
TYPE 연관배열이름 IS TABLE OF 자료형[NOT NULL]
INDEX BY 인덱스형;
```

- 자료형에는 VARCHAR2, DATE, NUMBER와 같인 다일 자료형, %TYPE, %ROWTYPE 같은 참조 자료형도 가능하다.
- 키로 사횽할 인덱스의 자료형은 BINARY_INTEGER, PLS_INTEGER같은 정수 또는 VARCHAR2같은 문자 자료형도 사용할 수 있다.

- 이렇게 정의된 연관배열은 레코드와 마찬가지로 특정 변수의 자료형으로서 사용할 수 있다.

```SQL
DECLARE
	TYPE ITAB_EX IS TABLE OF VARCHAR2(20)
INDEX BY PLS_INTEGER;
	
	text_arr ITAB_EX;

BEGIN
	text_arr(1) := '1st data';
	text_arr(2) := '2nd data';
	text_arr(3) := '3rd data';
	text_arr(4) := '4th data';
	
	DBMS_OUTPUT.PUT_LINE('text_arr(1) : ' || text_arr(1));
	DBMS_OUTPUT.PUT_LINE('text_arr(2) : ' || text_arr(2));
	DBMS_OUTPUT.PUT_LINE('text_arr(3) : ' || text_arr(3));
	DBMS_OUTPUT.PUT_LINE('text_arr(4) : ' || text_arr(4));
END;
/
```

#### 레코드를 활용한 연관 배열

- 연관 배열의 자료형에는 레코드를 사용할 수 있다.
- 이 경우에 다양한 자료형을 포함한 레코드를 여러 개 사용할 수 있으므로 마치 테이블과 같은 데이터 사용과 저장이 가능하다.

```SQL
DECLARE
	TYPE REC_DEPT IS RECORD(
    	deptno DEPT.DEPTNO%TYPE,
        dname DEPT.DNAME%TYPE
    );
    
    TYPE ITAB_DEPT IS TABLE OF REC_DEPT
    	INDEX BY PLS_INTEGER;
    
    dept_arr ITAB_DEPT;
    idx PLS_INTEGER := 0;
    
BEGIN
	FOR i IN (SELECT DEPTNO, DNAME FROM DEPT) LOOP
		idx := idx + 1;
		dept_arr(idx).deptno := i.DEPTNO;
		dept_arr(idx).dname := i.DNAME;
		
		DBMS_OUTPUT.PUT_LINE(
        	dept_arr(idx).deptno || ' : ' || dept_arr(idx).dname);
    END LOOP;
END;
/
```

- 만약 특정 테이브르이 전체 열과 같은 구성을 가진 연관 배열을 제작한다면 다음과 같이 %ROWTYPE을 사용하는 것이 레코드를 정의하는 것보다 편하다.

```SQL
DECLARE
	TYPE ITAB_DEPT IS TABLE OF DEPT%ROWTYPE
	INDEX BY PLS_INTEGER;
        
	dept_arr ITAB_DEPT;
    idx PLS_INTEGER := 0;
    
BEGIN
	FOR i IN (SELECT * FROM DEPT) LOOP
		idx := idx + 1;
		dept_arr(idx).deptno := i.DEPTNO;
		dept_arr(idx).dname := i.DNAME;
		dept_arr(idx).loc := i.LOC;
		
		DBMS_OUTPUT.PUT_LINE(
        	dept_arr(idx).deptno || ' : ' || dept_arr(idx).dname || ' : ' || dept_arr(idx).loc);
    END LOOP;
END;
/
```

### 컬렉션 메서드

- 오라클에서는 컬렉션 사용상의 편의를 위해 몇 가지 서브프로그램을 제공한다.
- 이를 컬렉션 메서드라고 하며 컬렉션 메서드는 컬렉션과 관련된 다양한 정보 조회 기능을 제공한다.
- 이와 더불어 컬렉션 내의 데이터 삭제나 컬렉션 크기 조절을 위한 특정 조작도 가능하다.
  - EXISTS(n) : 컬렉션에서 n인덱스의 데이터 존재여부를 true/false로 반환한다.
  - COUNT : 컬렉션에 포함되어 있는 요소 개수를 반환한다.
  - LIMIT : 현재 컬렉션의 최대 크기를 반환한다.
  - FIRST : 켈력션의 첫 번째 인덱스 번호를 반환한다.
  - LAST : 켈력션의 마지막 인덱스 번호를 반환한다.
  - PRIOR(n) : 컬렉션 n 인덱스 바로 앞 인덱스를 반환한다.
  - NEXT(n) : 컬렉션 n 인덱스 바로 뒤 인덱스를 반환한다.
  - DELETE : 컬렉션에 저장된 요소를 지운다.
  - EXTEND : 컬렉션 크기를 증가시킨다.
  - TRIM : 컬렉션 크기를 감소시킨다.

```SQL
DECLARE
	TYPE ITAB_EX IS TABLE OF VARCHAR2(20)
INDEX BY PLS_INTEGER;
	
	text_arr ITAB_EX;

BEGIN
	text_arr(1) := '1st data';
	text_arr(2) := '2nd data';
	text_arr(3) := '3rd data';
	text_arr(50) := '50th data';
	
	DBMS_OUTPUT.PUT_LINE('text_arr.COUNT : ' || text_arr.COUNT);
	DBMS_OUTPUT.PUT_LINE('text_arr.FIRST : ' || text_arr.FIRST);
	DBMS_OUTPUT.PUT_LINE('text_arr.LAST : ' || text_arr.LAST);
	DBMS_OUTPUT.PUT_LINE('text_arr.PRIOR(50) : ' || text_arr.PRIOR(50));
	DBMS_OUTPUT.PUT_LINE('text_arr.NEXT(50) : ' || text_arr.NEXT(50));
END;
/
```

