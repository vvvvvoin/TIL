# 커서와 예외 처리

- PL/SQL 내부에서 SQL문의 실행 및 결과를 사용할 수 있는 커서
- PL/SQL 문을 실행할 때 밠갱할 수 있는 오류를 처리하는 예외처리

## 특정 열을 선택하여 처리하는 커서

### 커서란?

- 커서는 SELECT문 또는 데이터 조작어 같은 SQL문을 실행했을 때 해당 SQL문을 처리하는 정보를 저장한 메로리 공간을 뜻한다.
- 커서를 사용하면 실행된 SQL문의 결과 값을 사용할 수 있다.
- SELECT문의 결과 값이 여러 행으로 나왔을 때 각 행별로 특정 작업을 수행하도록 기능을 구현하는 것이 가능하다.
- 커서의 사용 방법에 따라 명시적, 암시적 커서로 나뉜다.

- 커서 사용에 앞서 SELECT INTO문을 먼저 살펴본다

### SELECT INTO 방식

- SELECT INTO문은 조회되는 데이터가 단 하나의 행일 때 사용하는 방식이다.
- 커서는 결과 행이 하나이든 여러 개이든 사오간없이 사용할 수 있다.
- SELECT INTO문은 SELECT절에 명시한 각 열의 결과 값을 다음과 같이 변수에 대입해준다.

```SQL
SELECT 열1, 열2, ...., 열N INTO 변수1, 변수2, ..., 변수N
FROM ....
```

- SELECT절에 명시한 열과 INTO절에 명시한 변수는 그 개수와 자료형이 일치해야한다.
- 또한 INTO절을 제외한 나머지 부분은 SELECT문과 사용법이 같다

```SQL
DECLARE
	V_DEPT_ROW DEPT%ROWTYPE;
BEGIN
	SELECT DEPTNO, DNAME, LOC INTO V_DEPT_ROW
	FROM DEPT
	WHERE DEPTNO = 40;
	DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || V_DEPT_ROW.DEPTNO);
	DBMS_OUTPUT.PUT_LINE('DNAME : ' || V_DEPT_ROW.DNAME);
	DBMS_OUTPUT.PUT_LINE('LOC : ' || V_DEPT_ROW.LOC);
END;
/
```

- 데이터 조회의 결과 값은 하나인 경우보다 여러 개인 경우가 흔하며 결과 행이 하나일지 여러개일지 알 수 업슨ㄴ 경우도 존재하므로 대부분 커서를 활용한다.

### 명시적 커서

- 명시적 커서는 사용자가 직접 커서를 선언하고 사용하는 커서를 뜻한다.
- 커서는 다음 단계를 커서 사용한다
  - 커서선언 : 사용자가 직접 이름을 지정하여 사용할 커서를 SQL문과 함꼐 선언한다.
  - 커서 열기 : 커서를 선언할 떄 작성한 SQL문을 실행한다.
  - 커서에서 읽어온 데이터 사용 : 실행된 SQL문의 결과 행 정보를 하나씩 읽어 와서 변수에 저장한 후 필요한 작업을 수행한다.
  - 커서 닫기 : 모든 행의 사용이 끝나고 커서를 종료한다.
- PL/SQL문에서 커서를 작성하는 방법은 다음과 같다

```SQL
DECALRE
	CURSOR 커서이름 IS SQL문;
BEGIN
	OPEN 커서이름;
	FETCH 커서이름 INTO 변수
	CLOSE 커서이름;
END;
```

#### 하나의 행만 조회도는 경우

- 하나의 행만이 조회되는 SELECT문을 커서로 지정하여 사용할 경우 SELECT INTO문을 사용할 때보다 복잡한 단계를 작성해야하므로 번거로워 보인다.
- 커서의 효용성은 조회도는 행이 여러개일 때 극대화 된다.

```SQL
DECLARE
	V_DEPT_ROW DEPT%ROWTYPE;
	CURSOR c1 IS
		SELECT DEPTNO, DNAME, LOC
		FROM DEPT
		WHERE DEPTNO = 40;
BEGIN
	OPEN c1;
	FETCH c1 INTO V_DEPT_ROW;
	DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || V_DEPT_ROW.DEPTNO);
	DBMS_OUTPUT.PUT_LINE('DNAME : ' || V_DEPT_ROW.DNAME);
	DBMS_OUTPUT.PUT_LINE('LOC : ' || V_DEPT_ROW.LOC);
	
	CLOSE c1;
END;
/
```

#### 여러 행이 조회되는 경우 사용하는 LOOP문

- 커서에 지정한 SELECT문이 여러 행을 결과 값을 가질 경우에 여러 방식의 LOOP문을 사용할 수 있다.

```SQL
DECLARE
	V_DEPT_ROW DEPT%ROWTYPE;
	CURSOR c1 IS
		SELECT DEPTNO, DNAME, LOC
		FROM DEPT;
		
BEGIN
	OPEN c1;
	
	LOOP
		FETCH c1 INTO V_DEPT_ROW;
		EXIT WHEN c1%NOTFOUND;
		DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || V_DEPT_ROW.DEPTNO || ', DNAME : ' || V_DEPT_ROW.DNAME || ', LOC : ' || V_DEPT_ROW.LOC);
	END LOOP;
	
	CLOSE c1;
END;
/
```

- 여기서 %FOTFOUND는 실행된 FETCH문에서 행을 추출했으면 FALSE, 추출하지 않았으면 TRUE를 반환한다.
- FETCH문을 통해 더 이상 추출한 데이터가 없을 경우에 LOOP반복이 끝난다.
- %NOTFOUND외 몇가지 속성이 존재한다.
  - 커서이름%NOTFOUND : 수행된 FETCH문을 통해 추출된 행이 있으면 FALSE, 없으면 TRUE
  - 커서이름%FOUND : 수행된 FETCH문을 통해 추출된 행이 있으면 TRUE, 없으면 FALSE
  - 커서이름%ROWCOUNT : 현재까지 추출된 행 수를 반환
  - 커서이름%ISOPEN : 커서가 열러있으면 TRUE, 닫혀 있으면 FALSE반환

#### 여러 개의 행이 조회되는 경우(FOR LOOP문)

- LOOP문을 사용하여 커서를 처리하는 방ㅅ기은 커서 속성을 사용하여 반복 수행을 제어해야한다.
- 커서에 FOR LOOP문을 사용하면 좀 더 간편하게 여러 행을 다룰 수 있다.

```SQL
FOR 루프인덱스이름 IN 커서이름 LOOP
	결과 행별로 박본 수행할 작업;
END LOOP;
```

- 루프 인덱스는 커서에 저장된 각 행이 저장되는 변수를 뜻하며 ',' 을 통해 각 필드에 접근할 수 있다
- 루프 인덱스 이름이 c1_rec인 경우 c1_rec.DEPTNO는 SELECT문을 통해 조회된 데이터의 각 행에 해당하는 DEPTNO열의 데이터를 가리킨다.

```SQL
DECLARE
	CURSOR c1 IS
		SELECT DEPTNO, DNAME, LOC
		FROM DEPT;
		
BEGIN
	FOR c1_REC IN c1 LOOP
		DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || c1_REC.DEPTNO || ', DNAME : ' || c1_REC.DNAME || ', LOC : ' || c1_REC.LOC);
	END LOOP;
END;
/
```

- 커서 각 행을 c1_rec 루프 인덱스에 저장하므로 결과 행을 저장하는 변수 선언이 필요하지 않다. (자바의 인핸스드 FOR문과 같음)

#### 커서에 파라미터 사용하기

- 사용자가 직접 입력한 값 또는 상황에 따라 여러 값을 번갈아 사용하려면 다음과 같이 커서에 파라미터를 지정할 수 있다.

```SQL
CURSOR 커서이름(파라미터 이름 자료형, ...) IS
SELECT ...
```

```SQL
DECLARE
	V_DEPT_ROW DEPT%ROWTYPE;
	CURSOR c1(p_deptno DEPT.DEPTNO%TYPE) IS
		SELECT DEPTNO, DNAME, LOC
		FROM DEPT
		WHERE DEPTNO = p_deptno;
		
BEGIN
	OPEN c1(10);
	LOOP
		FETCH c1 INTO V_DEPT_ROW;
		EXIT WHEN c1%NOTFOUND;
		DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || V_DEPT_ROW.DEPTNO || ', DNAME : ' || V_DEPT_ROW.DNAME || ', LOC : ' || V_DEPT_ROW.LOC);
	END LOOP;
	CLOSE c1;
	
	OPEN c1(20);
	LOOP
		FETCH c1 INTO V_DEPT_ROW;
		EXIT WHEN c1%NOTFOUND;
		DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || V_DEPT_ROW.DEPTNO || ', DNAME : ' || V_DEPT_ROW.DNAME || ', LOC : ' || V_DEPT_ROW.LOC);
	END LOOP;
	CLOSE c1;
END;
/
```

- 만약 커서 실행에 필요한 파라미터 값을 사용자에게 직접 입력 받고 싶다면 & 기호를 치환 변수를 사용할 수 있다

```SQL
DECLARE
	v_deptno DEPT.DEPTNO%TYPE;
	CURSOR c1(p_deptno DEPT.DEPTNO%TYPE) IS
		SELECT DEPTNO, DNAME, LOC
		FROM DEPT
		WHERE DEPTNO = p_deptno;
		
BEGIN
	v_deptno := &INPUT;
	FOR c1_REC IN c1(v_deptno) LOOP
		DBMS_OUTPUT.PUT_LINE('DEPTNO : ' || c1_REC.DEPTNO || ', DNAME : ' || c1_REC.DNAME || ', LOC : ' || c1_REC.LOC);
	END LOOP;

END;
/
```

### 암시적 커서

- 암시적 커서는 별다른 선언 없이 SQL문을 사용했을 떄 오라클에서 자동으로 선언되는 커서를 뜻한다.
- 사용자가 OPEN, FETCH, CLOSE를 지정하지 않는다
- PL/SQL문 내부에서 DML명렁어나 SELECT INTO문 등이 실행될 때 자동으로 생성 및 처리된다.
- 자동으로 샹성되어 실행되는 암시적 커서는 별다른 PL/SQL문을 작성하지 않아도 되지만, 다음 묵시적 커서의 속성을 사용하면 현재 커서의 정보를 확ㅇ니할 수 있다.
- 커서가 자동으로 생성되므로 커서 이름을 지정하지 않고 SQL 키워드로 속성을 지정하며, 암시적 커서의 속성과 유사한 기능을 갖는다.
  - SQL%NOTFOUND : 수행된 FETCH문을 통해 추출된 행이 있으면 FALSE, 없으면 TRUE
  - SQL%FOUND : 수행된 FETCH문을 통해 추출된 행이 있으면 TRUE, 없으면 FALSE
  - SQL%ROWCOUNT : 현재까지 추출된 행 수를 반환
  - SQL%ISOPEN : 커서가 열러있으면 TRUE, 닫혀 있으면 FALSE반환

```SQL
BEGIN
	UPDATE DEPT SET DNAME = 'DATABASE'
	WHERE DEPTNO = 50;
	
	DBMS_OUTPUT.PUT_LINE('갱신된 행의 수 : ' || SQL%ROWCOUNT);
	
	IF(SQL%FOUND) THEN
		DBMS_OUTPUT.PUT_LINE('갱신 대상 행 존재 여부 : TRUE');
	ELSE
		DBMS_OUTPUT.PUT_LINE('갱신 대상 행 존재 여부 : FALSE');
	END IF;
	
	IF(SQL%ISOPEN) THEN
		DBMS_OUTPUT.PUT_LINE('커서의 OPEN 여부 : TRUE');
	ELSE
		DBMS_OUTPUT.PUT_LINE('커서의 OPEN 여부 : FALSE');
	END IF;
END;
/
```

## 오류가 발생해도 프로그램이 비정상 종료되지 않도록 하는 예외 처리

### 오류란?

- 오라클에서 SQL, PL.SQL이 정상 수행되지 못하는 상황을 오류라고 한다.
- 오류는 크게 두가지로 나뉜다.
- 하나는 문법이 잘못되었거나 오타로 인한 오류로 컴파일 오류, 문법 오류가 있다
- 다른 하나는 명령문의 실행 중 발생한 오류가 있고, 이를 런타임 오류 또는 실행 오류라고 부른다.
- 오라클에서는 이 두가지 오류 중 후자, 프로그램이 실행되는 도중 발생하는 오류를 예외라고 한다.

- 다음 예제는 예외가 발생한다.

```SQL
DECLARE
	v_wrong NUMBER;
BEGIN
	SELECT DNAME INTO v_wrong
	FROM DEPT
	WHERE DEPTNO = 10;
END;
/
```

- 문자열 데이터를 숫자 자료형 변수에 대입하려고 했기 때문에 해당 PL/SQL문은 예외가발생하고 비정상 종료된다.
- 이처럼 PL/SQL 실행 중 예외가 발생했을 떄 프로그램이 비정상 종료되는 것을 막기 위해 특정 명령어를 PL.SQL문 안에 작성하는데 이를 예외처리라 한다.
- 예외 처리는 PL/SQL문 안에 EXCEPTION영역에 필요 코드를 작성하는 것을 뜻한다.

```SQL
DECLARE
	v_wrong NUMBER;
BEGIN
	SELECT DNAME INTO v_wrong
	FROM DEPT
	WHERE DEPTNO = 10;
EXCEPTION
	WHEN VALUE_ERROR THEN
		DBMS_OUTPUT.PUT_LINE('예외 처리 : 수치 또는 값 오류 발생');
END;
/
```

- 예외가 발생했지만 PL/SQL문은 정상 처리되었을을 확인할 수 있다.
- 이 예외 처리부가 실행되면 예외가 발생한 코드 이후에 내용은 실행 되지 않는다.

### 예외 종류

- 오라클에서 예외는 크게 내부 예외와 사용자 정의 예외로 나뉜다.
- 내부 예외는 오라클에서 미리 정의한 예외를 뜻하며 사용자 정의 예외는 사용자가 필요에 따라 추가로 정의한 예외를 의마한다.
- 내부예외는 이름이 정의되어 있는 예외인 사전 정의된 예외와 이름이 정해지지 않은 예외로 다시 나뉜다.
  - 내부 예외
    - 사전 정의된 예외 : 내부 예외 중 예외 번호에 해당하는 이름이 존재하는 예외
    - 이름이 업슨 예외 : 내부  예외 중 이름이 존재하지 않는 예외
  - 사용자 정의 예외 : 사용자가 필요에 따라 직접 정의한 예외

### 예외 처리부 작성

- 예외 처리부는 앞의 예제에서 보았듯이 EXCEPTION절에 필요한 코드를 사용하여 작성한다.
- WHEN으로 시작하는 절을 예외 핸들러라고 하며 발생한 예외 이름과 일차하는 WHEN절의 명령어를 수행한다.
- 수행할 명령어는 PL/SQL 실행부와 마찬가지로 여러 문법을 사용할 수 있다.
- OTHERS는 먼저 작성한 어느 예외와도 일치하는 예외가 없을 경우에 처리할 내용을 작성한다.

```SQL
EXCEPTION
	WHEN 예외이름1 [OR 예외이름2 - ]THEN
		예외 처리에 사용할 명령어;
	WHEN 예외이름3 [OR 예외이름4 - ]THEN
		예외 처리에 사용할 명령어;
	...
	WHEN OTHERS THEN
		예외 처리에 사용할 명령어;
```

#### 사전 정의된 예외 사용

- 예외 핸들러에 사전 정의된 예외만을 사용할 때는 앞서 살펴본 작성 방식대로 발생할 수 있는 예외를 명시한다.

```SQL
DECLARE
	v_wrong NUMBER;
BEGIN
	SELECT DNAME INTO v_wrong
	FROM DEPT
	WHERE DEPTNO = 10;
	
EXCEPTION
	WHEN TOO_MANY_ROWS THEN
		DBMS_OUTPUT.PUT_LINE('예외 처리 : 요구보다 많은 행 추출 오류 발생');
	WHEN VALUE_ERROR THEN
		DBMS_OUTPUT.PUT_LINE('예외 처리 : 수치 또는 값 오류 발생');
	WHEN OTHERS THEN
		DBMS_OUTPUT.PUT_LINE('예외 처리 : 사용자 정의 외 오류 발생');
END;
/
```

#### 이름 없는 예외 사용

- 만약 이름이 없는 내부 예외를 사용해야 한다면 이름을 직접 지정해 주어야 예외 처리부에서 사용할 수 있다.
- 이름을 직접 지어 줄 떄 다음과 같이 선언부에서 오라클 예외 번호와 함께 이름을 붙인다.
- 이름이 정해진 예외는 사전 정의된 예외를 사용할 떄와 마찬가지로 예외 처리부에서 지정한 이름으로 예외 핸들러에 작성한다.

```sql
DECLARE
	예외이름1 EXCEPTION;
	PRAGMA EXCEPTION_INIT(예외이름1, 예외번호);
.
.
EXCEPTION
	WHEN 예외이름1 WHEN
		예외 처리에 사용할 명령어;
	...
END;
```

#### 사용자 정의 예외 사용

- 사용자 정의 예외는 오라클에 정의되어 있지 않은 특정 상황을 직접 오류로 정의하는 방식이다.
- 다음과 같이 예외 이름을 정해 주고 실행부에서 직접 정의한 오류 상황이 생셨을 때 RAISE 키워드를 사용하여 예외를 직접 만들 수 있다.
- 이렇게 직접 만든 예외 역시 앞의 예외 처리와 마찬가지로 예외 처리부에서 예외 이름을 통해 수행할 내용을 작성해 줌으로써 처리한다.

```SQL
DECLARE
	사용자 예외 이름 EXCEPTION;
	...
BEGIN
	IF 사용자 예외를 발생시킬 조건 THEN
		RAISE 사용자 예외 이름
	...
	END IF;
EXCEPTION
	WHEN 사용자 예외 이름 THEN
		예외 처리에 사용할 명령어;
	...
END;
```

#### 오류 코드와 오류 메시지 사용

- 오류 처리부가 잘 작성되어 있다면 오류가 발생해도 PL/SQL은 정상 종료된다.
- PL/SQL문의 정상 종료 여부와 상관없이 발생한 오류 내역을 알고 싶을 때 SQLCODE, SQLERRM함수를 사용한다.

```SQL
DECLARE
	v_wrong NUMBER;
BEGIN
	SELECT DNAME INTO v_wrong
	FROM DEPT
	WHERE DEPTNO = 10;
	
EXCEPTION
	WHEN OTHERS THEN
		DBMS_OUTPUT.PUT_LINE('예외 처리 : 사전 정의 외 오류 발생');
		DBMS_OUTPUT.PUT_LINE('SQLCODE : ' || TO_CHAR(SQLCODE));
		DBMS_OUTPUT.PUT_LINE('SQLERRM : ' || SQLERRM);
END;
/
```

> SQLCODE, SQLERRM은 PL/SQL에서만 사용 가능한 함수로 SQL문에서는 사용할 수 없다.